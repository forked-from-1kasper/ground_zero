import GroundZero.HITs.Suspension
import GroundZero.Theorems.Equiv
import GroundZero.Types.Integer

open GroundZero.HITs.Interval
open GroundZero.Types.Equiv
open GroundZero.Structures
open GroundZero.Types.Id
open GroundZero.Types

/-
  Circle SÂ¹ as Higher Inductive Type.
  * HoTT 6.4

  Ï€â‚(SÂ¹) = â„¤ proof.
  * HoTT 8.1
-/

namespace GroundZero
namespace HITs

universe u v w

hott def suspEmpty : âˆ‘ ğŸ â‰ƒ ğŸ :=
let f : âˆ‘ ğŸ â†’ ğŸ :=
Suspension.rec false true Proto.Empty.elim
let g : ğŸ â†’ âˆ‘ ğŸ :=
Î» | false => Suspension.north
  | true  => Suspension.south;
begin
  existsi f; apply Prod.mk <;> existsi g;
  { intro z; induction z; reflexivity; reflexivity;
    apply Proto.Empty.elim; assumption };
  { intro b; induction b using Bool.casesOn <;> reflexivity }
end

namespace Loop
  variable {A : Type u} {a : A}

  def pos (p : a = a) : â„• â†’ a = a
  | Nat.zero   => idp a
  | Nat.succ n => pos p n â¬ p

  def neg (p : a = a) : â„• â†’ a = a
  | Nat.zero   => pâ»Â¹
  | Nat.succ n => neg p n â¬ pâ»Â¹

  def power (p : a = a) : â„¤ â†’ a = a
  | Integer.pos n => pos p n
  | Integer.neg n => neg p n

  hott def powerComp (p : a = a) : Î  z, power p z â¬ p = power p (Integer.succ z)
  | Integer.neg Nat.zero     => Id.invComp _
  | Integer.neg (Nat.succ n) => (Id.assoc _ _ _)â»Â¹ â¬ Id.map (neg p n â¬ Â·) (Id.invComp _) â¬ Id.reflRight _
  | Integer.pos _            => idp _

  hott def powerCompPred (p : a = a) : Î  z, power p z â¬ pâ»Â¹ = power p (Integer.pred z)
  | Integer.neg _            => idp _
  | Integer.pos Nat.zero     => idp _
  | Integer.pos (Nat.succ n) => (Id.assoc _ _ _)â»Â¹ â¬ Id.map (pos p n â¬ Â·) (Id.compInv _) â¬ Id.reflRight _

  hott def compPowerPos (p : a = a) : Î  n, p â¬ power p (Integer.pos n) = power p (Integer.succ (Integer.pos n))
  | Nat.zero   => Id.reflRight _
  | Nat.succ n => Id.assoc _ _ _ â¬ Id.map (Â· â¬ p) (compPowerPos p n)

  hott def compPowerNeg (p : a = a) : Î  n, p â¬ power p (Integer.neg n) = power p (Integer.succ (Integer.neg n))
  | Nat.zero   => Id.compInv _
  | Nat.succ n => begin
    transitivity; apply Id.assoc;
    symmetry; apply Equiv.invCompRewrite;
    symmetry; transitivity; apply compPowerNeg p n;
    symmetry; apply powerComp
  end

  hott def compPower (p : a = a) : Î  z, p â¬ power p z = power p (Integer.succ z)
  | Integer.neg n => compPowerNeg p n
  | Integer.pos m => compPowerPos p m

  hott def compPowerPred (p : a = a) : Î  z, pâ»Â¹ â¬ power p z = power p (Integer.pred z)
  | Integer.neg Nat.zero     => idp _
  | Integer.neg (Nat.succ n) => begin apply Equiv.rewriteComp; symmetry; apply compPower end
  | Integer.pos Nat.zero     => Id.reflRight _
  | Integer.pos (Nat.succ n) => begin apply Equiv.rewriteComp; symmetry; apply compPower end

  hott def compPowerComm (p : a = a) (z : â„¤) :
    p â¬ power p z = power p z â¬ p :=
  compPower p z â¬ (powerComp p z)â»Â¹

  hott def invCompPowerComm (p : a = a) (z : â„¤) :
    pâ»Â¹ â¬ power p z = power p z â¬ pâ»Â¹ :=
  compPowerPred p z â¬ (powerCompPred p z)â»Â¹

  hott def powerComm (p : a = a) (x y : â„¤) : power p x â¬ power p y = power p y â¬ power p x :=
  begin
    fapply @Integer.indsp (Î» x, Î  y, power p x â¬ power p y = power p y â¬ power p x) _ _ _ x <;> clear x;
    { intro y; symmetry; apply Id.reflRight };
    { intros x ih y; transitivity; apply Id.map (Â· â¬ power p y);
      symmetry; apply compPower;
      transitivity; symmetry; apply Id.assoc;
      transitivity; apply Id.map; apply ih;
      transitivity; apply Id.assoc;
      transitivity; apply Id.map (Â· â¬ power p x); apply compPowerComm;
      transitivity; symmetry; apply Id.assoc;
      apply Id.map; apply compPower };
    { intros x ih y; transitivity; apply Id.map (Â· â¬ power p y);
      symmetry; apply compPowerPred;
      transitivity; symmetry; apply Id.assoc;
      transitivity; apply Id.map; apply ih;
      transitivity; apply Id.assoc;
      transitivity; apply Id.map (Â· â¬ power p x);
      apply invCompPowerComm;
      transitivity; symmetry; apply Id.assoc;
      apply Id.map; apply compPowerPred }
  end
end Loop

hott def S : â„• â†’ Type
| Nat.zero   => ğŸ
| Nat.succ n => âˆ‘ (S n)

def S.lift : Î  n, S n â†’ S (n + 1)
| Nat.zero,   false => Suspension.north
| Nat.zero,   true  => Suspension.south
| Nat.succ _, z     => Suspension.rec Suspension.north Suspension.south (Î» _, Suspension.merid z) z

macro:max "S" n:superscript : term => do `(GroundZero.HITs.S $(â† Meta.Notation.parseSuperscript n))
instance (n : â„•) : Id.dotted Sâ¿ :=
âŸ¨match n with
 | Nat.zero   => false
 | Nat.succ _ => Suspension.northâŸ©

macro:max "S" : term => `(GroundZero.HITs.S)

def Circle := SÂ¹

namespace Circle
  -- https://github.com/leanprover/lean2/blob/master/hott/homotopy/Circle.hlean

  def baseâ‚ : SÂ¹ := Suspension.north
  def baseâ‚‚ : SÂ¹ := Suspension.south

  def segâ‚ : baseâ‚ = baseâ‚‚ := Suspension.merid false
  def segâ‚‚ : baseâ‚ = baseâ‚‚ := Suspension.merid true

  hott def indâ‚‚ {B : SÂ¹ â†’ Type u} (bâ‚ : B baseâ‚) (bâ‚‚ : B baseâ‚‚)
    (â„“â‚ : bâ‚ =[segâ‚] bâ‚‚) (â„“â‚‚ : bâ‚ =[segâ‚‚] bâ‚‚) : Î  x, B x :=
  Suspension.ind bâ‚ bâ‚‚ (Î» | false => â„“â‚ | true => â„“â‚‚)

  def base : SÂ¹ := baseâ‚
  def loop : base = base := segâ‚‚ â¬ segâ‚â»Â¹

  hott def rec {B : Type u} (b : B) (â„“ : b = b) : SÂ¹ â†’ B :=
  Suspension.rec b b (Î» | false => idp b | true => â„“)

  hott def recÎ²ruleâ‚ {B : Type u} (b : B) (â„“ : b = b) : rec b â„“ base = b :=
  idp b

  -- why this doesnâ€™t require â€œnoncomputableâ€ attribute as it was in LeanÂ 3?
  -- looks pretty strange
  hott def recÎ²ruleâ‚‚ {B : Type u} (b : B) (â„“ : b = b) := calc
          Id.map (rec b â„“) loop
        = Id.map (rec b â„“) segâ‚‚ â¬ Id.map (rec b â„“) segâ‚â»Â¹   : Equiv.mapFunctoriality _
    ... = Id.map (rec b â„“) segâ‚‚ â¬ (Id.map (rec b â„“) segâ‚)â»Â¹ : Id.map (_ â¬ Â·) (Id.mapInv _ _)
    ... = â„“ â¬ Id.reflâ»Â¹                                     : bimap (Â· â¬ Â·â»Â¹) (Suspension.recÎ²rule _ _ _ _) (Suspension.recÎ²rule _ _ _ _)
    ... = â„“                                                 : Id.reflRight _

  hott def recÎ²ruleâ‚ƒ {B : Type u} (b : B) (â„“ : b = b) := calc
            Id.map (rec b â„“) loopâ»Â¹
          = (Id.map (rec b â„“) loop)â»Â¹ : Id.mapInv _ _
      ... = â„“â»Â¹                       : Id.map Id.inv (recÎ²ruleâ‚‚ _ _)

  hott def ind {B : SÂ¹ â†’ Type u} (b : B base) (â„“ : b =[loop] b) : Î  (x : SÂ¹), B x :=
  indâ‚‚ b (Equiv.subst segâ‚ b) Id.refl (depPathTransSymm â„“)

  attribute [eliminator] ind

  hott def indÎ²ruleâ‚ {B : SÂ¹ â†’ Type u} (b : B base) (â„“ : b =[loop] b) : ind b â„“ base = b :=
  idp b

  hott def indÎ²ruleâ‚‚ {B : SÂ¹ â†’ Type u} (b : B base) (â„“ : b =[loop] b) : apd (ind b â„“) loop = â„“ :=
  begin
    dsimp [ind, indâ‚‚];
    transitivity; apply apdFunctoriality;
    transitivity; apply bimap depTrans; apply Suspension.indÎ²rule;
    transitivity; apply apdInv; apply Id.map;
    apply Suspension.indÎ²rule; apply depPathTransSymmCoh
  end

  hott def indÎ© {B : SÂ¹ â†’ Type u} (b : B base) (H : Î  x, prop (B x)) : Î  x, B x :=
  begin fapply ind; exact b; apply H end

  noncomputable hott def loopEqReflImplsUip {A : Type u} (H : loop = idp base) : K A :=
  begin
    intros a p; transitivity;
    symmetry; apply Circle.recÎ²ruleâ‚‚ a p;
    change _ = Id.map (rec a p) (idp _);
    apply Id.map; apply H
  end

  noncomputable hott def loopNeqRefl : Â¬(loop = idp base) :=
  begin
    intro H; apply ua.universeNotASet;
    intros A B p q; apply (KIffSet Type).left;
    apply loopEqReflImplsUip; assumption
  end

  namespace map
    def trivial    : SÂ¹ â†’ SÂ¹ := rec base (idp base)
    def nontrivial : SÂ¹ â†’ SÂ¹ := rec base loop

    noncomputable hott def trivialNotHmtpy : Â¬(trivial = id) :=
    begin
      intro p; apply loopNeqRefl; transitivity; symmetry; apply idmap;
      apply transport (Î» f, Id.map f loop = idp (f base)) p; apply Circle.recÎ²ruleâ‚‚
    end

    noncomputable hott def trivialHmtpy : trivial ~ (Î» _, base) :=
    begin
      intro x; induction x; reflexivity; apply Id.trans; apply transportOverContrMap;
      transitivity; apply Id.map (Â· â¬ idp base); transitivity; apply Id.mapInv;
      apply Id.map; apply recÎ²ruleâ‚‚; reflexivity
    end

    noncomputable hott def nontrivialHmtpy : nontrivial ~ id :=
    begin
      intro x; induction x; reflexivity;
      apply Id.trans; apply transportOverInvolution;
      transitivity; apply Id.map (Â· â¬ idp base â¬ loop);
      transitivity; apply Id.mapInv; apply Id.map; apply recÎ²ruleâ‚‚;
      transitivity; symmetry; apply Id.assoc; apply Id.invComp
    end

    noncomputable hott def nontrivialNotHmtpy : Â¬(nontrivial = (Î» _, base)) :=
    Î» p, trivialNotHmtpy (Theorems.funext trivialHmtpy â¬ pâ»Â¹ â¬
                          Theorems.funext nontrivialHmtpy)
  end map

  def succ (l : Î©Â¹(SÂ¹)) : Î©Â¹(SÂ¹) := l â¬ loop
  def pred (l : Î©Â¹(SÂ¹)) : Î©Â¹(SÂ¹) := l â¬ loopâ»Â¹

  def zero   := idp base
  def one    := succ zero
  def two    := succ one
  def three  := succ two
  def fourth := succ three

  hott def helix : SÂ¹ â†’ Type :=
  rec â„¤ (ua Integer.succEquiv)

  def power : â„¤ â†’ Î©Â¹(SÂ¹) := Loop.power loop

  hott def encode (x : SÂ¹) (p : base = x) : helix x :=
  transport helix p (Integer.pos 0)

  example : power 2 = loop â¬ loop :=
  by reflexivity

  hott def winding : base = base â†’ â„¤ := encode base

  noncomputable hott def transportThere (x : â„¤) := calc
          transport helix loop x
        = transportconst (Id.map helix loop) x    : Equiv.transportComp id helix loop x
    ... = transportconst (ua Integer.succEquiv) x : Id.map (transportconst Â· x) (recÎ²ruleâ‚‚ _ _)
    ... = Integer.succ x                          : ua.transportRule _ _

  noncomputable hott def transportBack (x : â„¤) := calc
           transport helix loopâ»Â¹ x
         = transportconst (Id.map helix loopâ»Â¹) x    : Equiv.transportComp id helix loopâ»Â¹ x
     ... = transportconst (Id.map helix loop)â»Â¹ x    : Id.map (transportconst Â· x) (Id.mapInv _ _)
     ... = transportconst (ua Integer.succEquiv)â»Â¹ x : Id.map (transportconst Â·â»Â¹ x) (recÎ²ruleâ‚‚ _ _)
     ... = Integer.pred x                            : ua.transportInvRule _ _

  noncomputable hott def decode (x : SÂ¹) : helix x â†’ base = x :=
  begin
    induction x; exact power; apply Theorems.funext; intro x;
    transitivity; apply Homotopy.Id (transportCharacterization power loop) x;
    transitivity; apply transportComposition;
    transitivity; apply Id.map (power Â· â¬ loop); apply transportBack;
    transitivity; apply Id.map (Â· â¬ loop);
    transitivity; symmetry; apply Loop.compPowerPred; apply Loop.invCompPowerComm;
    apply Id.cancelInvComp
  end

  noncomputable hott def decodeEncode (x : SÂ¹) (p : base = x) : decode x (encode x p) = p :=
  begin induction p; reflexivity end

  noncomputable hott def powerOfWinding : power âˆ˜ winding ~ id :=
  decodeEncode base

  noncomputable hott def windingPos : Î  n, winding (power (Integer.pos n)) = Integer.pos n
  | Nat.zero   => idp _
  | Nat.succ n => substOverPathComp _ _ _ â¬ transportThere _ â¬ Id.map _ (windingPos n)

  noncomputable hott def windingNeg : Î  n, winding (power (Integer.neg n)) = Integer.neg n
  | Nat.zero   => transportBack _
  | Nat.succ n => substOverPathComp _ _ _ â¬ transportBack _ â¬ Id.map _ (windingNeg n)

  noncomputable hott def windingPower : Î  z, winding (power z) = z
  | Integer.neg n => windingNeg n
  | Integer.pos n => windingPos n

  noncomputable hott def encodeDecode (x : SÂ¹) : Î  c, encode x (decode x c) = c :=
  begin induction x; intro c; apply windingPower; apply Theorems.funext; intro z; apply Integer.set end

  noncomputable hott def family (x : SÂ¹) : (base = x) â‰ƒ helix x :=
  âŸ¨encode x, (âŸ¨decode x, decodeEncode xâŸ©, âŸ¨decode x, encodeDecode xâŸ©)âŸ©

  noncomputable hott def fundamentalGroup : Î©Â¹(SÂ¹) = â„¤ := ua (family base)

  noncomputable hott def loopHset : hset (base = base) :=
  transport hset fundamentalGroupâ»Â¹ Integer.set

  noncomputable example : winding (loop â¬ loop) = 2 := windingPower 2
  noncomputable example : winding loop = 1          := windingPower 1
  noncomputable example : winding loopâ»Â¹ = -1       := windingPower (-1)

  hott def rot : Î  (x : SÂ¹), x = x :=
  Circle.ind Circle.loop (begin
    apply Id.trans; apply transportInvCompComp; change _ = idp _ â¬ loop;
    apply Id.map (Â· â¬ loop); apply Id.invComp
  end)

  def Î¼â‚‘ : SÂ¹ â†’ SÂ¹ â‰ƒ SÂ¹ :=
  Circle.rec (ideqv SÂ¹) (Sigma.prod (Theorems.funext rot) (Theorems.Equiv.biinvProp _ _ _))

  def Î¼ (x : SÂ¹) : SÂ¹ â†’ SÂ¹ := (Î¼â‚‘ x).forward

  noncomputable hott def Î¼Loop : Id.map Î¼ loop = Theorems.funext rot :=
  begin
    transitivity; apply mapOverComp;
    transitivity; apply Id.map; apply recÎ²ruleâ‚‚;
    apply Sigma.mapFstOverProd
  end

  hott def inv : SÂ¹ â†’ SÂ¹ := Circle.rec base loopâ»Â¹

  noncomputable hott def invInv (x : SÂ¹) : inv (inv x) = x :=
  let invâ‚š := @Id.map SÂ¹ SÂ¹ base base (inv âˆ˜ inv);
  begin
    induction x; reflexivity; apply calc
              transport (Î» x, inv (inv x) = x) loop Id.refl
            = invâ‚š loopâ»Â¹ â¬ Id.refl â¬ loop          : transportOverInvolution _ _ _
        ... = invâ‚š loopâ»Â¹ â¬ (Id.refl â¬ loop)        : (Id.assoc _ _ _)â»Â¹
        ... = Id.map inv (Id.map inv loopâ»Â¹) â¬ loop : Id.map (Â· â¬ loop) (mapOverComp _ _ _)
        ... = Id.map inv (Id.map inv loop)â»Â¹ â¬ loop : Id.map (Id.map inv Â· â¬ loop) (Id.mapInv inv loop)
        ... = Id.map inv loopâ»Â¹â»Â¹ â¬ loop            : @Id.map Î©Â¹(SÂ¹) _ _ _ (Id.map inv Â·â»Â¹ â¬ loop) (Circle.recÎ²ruleâ‚‚ base loopâ»Â¹)
        ... = Id.map inv loop â¬ loop                : @Id.map Î©Â¹(SÂ¹) _ _ _ (Id.map inv Â· â¬ loop) (Id.invInv _)
        ... = loopâ»Â¹ â¬ loop                         : Id.map (Â· â¬ loop) (Circle.recÎ²ruleâ‚‚ _ _)
        ... = idp base                              : Id.invComp _
  end

  hott def unitLeft (x : SÂ¹) : Î¼ base x = x := idp x

  hott def Î¼Right : Id.map (Î¼ base) loop = loop := Equiv.idmap _

  noncomputable hott def Î¼Left := calc
          Id.map (Î¼ Â· base) loop
        = happly (Id.map Î¼ loop) base         : Interval.mapHapply _ _
    ... = (happly âˆ˜ Theorems.funext) rot base : Id.map (Î» f, happly f base) Î¼Loop
    ... = loop                                : happly (Theorems.happlyFunext _ _ rot) base

  noncomputable hott def unitRight (x : SÂ¹) : Î¼ x base = x :=
  begin
    induction x; reflexivity; change _ = _;
    transitivity; apply transportOverInvolution (Î¼ Â· base);
    transitivity; apply Id.map (Â· â¬ idp base â¬ loop);
    transitivity; apply Id.mapInv; apply Id.map;
    apply Î¼Left; transitivity; apply Id.map (Â· â¬ loop);
    apply Id.reflRight; apply Id.invComp
  end

  hott def Î¼LeftApLem {x : SÂ¹} (p : base = x) :
    Id.map (Î¼ Â· base) p = transport (base = Â·) (unitRight x)â»Â¹ p :=
  begin induction p; reflexivity end

  hott def Î¼LeftAp  (p : Î©Â¹(SÂ¹)) : Id.map (Î¼ Â· base) p = p := Î¼LeftApLem p
  hott def Î¼RightAp (p : Î©Â¹(SÂ¹)) : Id.map (Î¼ base)   p = p := Equiv.idmap p

  noncomputable hott def unitComm (x : SÂ¹) : Î¼ base x = Î¼ x base := (unitRight x)â»Â¹

  noncomputable hott def mulInv (x : SÂ¹) : base = Î¼ x (inv x) :=
  begin
    induction x; exact loop; change _ = _;
    transitivity; apply transportComp (base = Â·) (AS Î¼ inv) loop;
    transitivity; apply transportComposition;
    transitivity; apply Id.map; apply Equiv.mapOverAS;
    transitivity; apply Id.map; apply Id.map; apply Circle.recÎ²ruleâ‚‚;
    transitivity; apply Id.map (Â· â¬ Equiv.bimap Î¼ loop loopâ»Â¹);
    symmetry; apply Î¼Right; symmetry; transitivity;
    symmetry; apply Î¼Left; apply bimapComp
  end

  -- https://github.com/mortberg/cubicaltt/blob/master/examples/helix.ctt#L207
  hott def lemSetTorus {Ï€ : SÂ¹ â†’ SÂ¹ â†’ Type u} (setÏ€ : hset (Ï€ base base))
    (f : Î  y, Ï€ base y) (g : Î  x, Ï€ x base) (p : f base = g base) : Î  x y, Ï€ x y :=
  begin
    intro x; induction x; exact f; change _ = _; transitivity;
    apply transportOverPi; apply Theorems.funext; intro y; induction y;
    transitivity; apply Id.map; exact p; transitivity; apply apd; exact pâ»Â¹; apply setÏ€
  end

  noncomputable hott def isGroupoid : groupoid SÂ¹ :=
  begin
    intros a b; change hset (a = b);
    fapply @indÎ© (Î» a, Î  b, hset (a = b)) _ _ a <;> clear a;
    { intro b; fapply @indÎ© (Î» b, hset (base = b)) _ _ b <;> clear b;
      apply loopHset; intro; apply Structures.setIsProp };
    intro; apply piProp; intro; apply Structures.setIsProp
  end

  noncomputable hott def mulComm (x y : SÂ¹) : Î¼ x y = Î¼ y x :=
  begin
    fapply @lemSetTorus (Î» x y, Î¼ x y = Î¼ y x); apply loopHset;
    { intro z; symmetry; apply unitRight };
    { intro z; apply unitRight }; reflexivity
  end

  noncomputable hott def mulAssoc : Î  x y z, Î¼ x (Î¼ y z) = Î¼ (Î¼ x y) z :=
  begin
    intro x; fapply @lemSetTorus (Î» y z, Î¼ x (Î¼ y z) = Î¼ (Î¼ x y) z); apply isGroupoid;
    { intro z; apply Id.map (Î¼ Â· z); exact (unitRight x)â»Â¹ };
    { intro z; transitivity; apply Id.map; apply unitRight; symmetry; apply unitRight };
    { induction x; reflexivity; apply isGroupoid }
  end

  noncomputable hott def mulTrans (p q : Î©Â¹(SÂ¹)) : bimap Î¼ p q = p â¬ q :=
  begin
    transitivity; apply bimapCharacterization;
    apply bimap; apply Î¼LeftAp; apply Î¼RightAp
  end

  noncomputable hott def mulTrans' (p q : Î©Â¹(SÂ¹)) : bimap Î¼ p q = q â¬ p :=
  begin
    transitivity; apply bimapCharacterization';
    apply bimap; apply Î¼RightAp; apply Î¼LeftAp
  end

  noncomputable hott def comm (x y : Î©Â¹(SÂ¹)) : x â¬ y = y â¬ x :=
  (mulTrans x y)â»Â¹ â¬ (mulTrans' x y)

  noncomputable hott def comm' (x y : Î©Â¹(SÂ¹)) : x â¬ y = y â¬ x :=
    Equiv.bimap Id.trans (powerOfWinding x)â»Â¹ (powerOfWinding y)â»Â¹
  â¬ Loop.powerComm Circle.loop (winding x) (winding y)
  â¬ Equiv.bimap Id.trans (powerOfWinding y) (powerOfWinding x)

  noncomputable hott def Î©indâ‚ {Ï€ : (Î©Â¹(SÂ¹)) â†’ Type u}
    (zeroÏ€ : Ï€ (idp base)) (succÏ€ : Î  x, Ï€ x â†’ Ï€ (x â¬ loop))
    (predÏ€ : Î  x, Ï€ x â†’ Ï€ (x â¬ loopâ»Â¹)) : Î  x, Ï€ x :=
  begin
    intro x; apply transport Ï€; apply powerOfWinding;
    fapply @Types.Integer.indsp (Ï€ âˆ˜ power) _ _ _ (winding x);
    { exact zeroÏ€ };
    { intros x ih; apply transport Ï€;
      apply Loop.powerComp Circle.loop;
      apply succÏ€; exact ih };
    { intros x ih; apply transport Ï€;
      apply Loop.powerCompPred;
      apply predÏ€; exact ih }
  end

  noncomputable hott def Î©indâ‚‚ {Ï€ : (Î©Â¹(SÂ¹)) â†’ Type u}
    (zeroÏ€ : Ï€ Id.refl) (succÏ€ : Î  x, Ï€ x â†’ Ï€ (loop â¬ x))
    (predÏ€ : Î  x, Ï€ x â†’ Ï€ (loopâ»Â¹ â¬ x)) : Î  x, Ï€ x :=
  begin
    fapply Î©indâ‚; exact zeroÏ€;
    { intros x ih; apply transport Ï€; apply comm; apply succÏ€; exact ih };
    { intros x ih; apply transport Ï€; apply comm; apply predÏ€; exact ih }
  end

  noncomputable hott def transComm {z : SÂ¹} : Î  (p q : z = z), p â¬ q = q â¬ p :=
  begin
    induction z; apply comm;
    apply Theorems.funext; intro;
    apply Theorems.funext; intro;
    apply isGroupoid
  end

  noncomputable hott def transportOverCircle {z : SÂ¹} {f g : SÂ¹ â†’ SÂ¹} {p : f = g}
    (Î¼ : f z = f z) (Î· : f z = g z) : @transport (SÂ¹ â†’ SÂ¹) (Î» Ï†, Ï† z = Ï† z) f g p Î¼ = Î·â»Â¹ â¬ Î¼ â¬ Î· :=
  begin induction p; symmetry; apply idConjIfComm; apply transComm end

  def halfway.Ï† : I â†’ SÂ¹ :=
  Î» i, Interval.elim loop (i âˆ§ Interval.neg i)

  def halfway : base = base :=
  Interval.lam halfway.Ï†

  hott def halfway.const : halfway.Ï† ~ Î» _, base :=
  begin
    intro x; induction x; reflexivity; reflexivity; change _ = _;
    transitivity; apply transportOverContrMap;
    transitivity; apply Id.reflRight;
    transitivity; apply Id.mapInv;
    transitivity; apply Id.map; apply mapOverComp;
    transitivity; apply Id.map; apply Id.map (map (elim loop));
    change _ = idp 0; apply Structures.propIsSet;
    apply Interval.intervalProp; reflexivity
  end

  hott def halfway.trivial : halfway = idp base :=
  begin
    transitivity; apply Equiv.mapWithHomotopy; apply halfway.const;
    transitivity; apply Id.reflRight; apply constmap
  end

  def natPow (x : SÂ¹) : â„• â†’ SÂ¹
  | Nat.zero   => base
  | Nat.succ n => Î¼ x (natPow x n)

  def pow (x : SÂ¹) : â„¤ â†’ SÂ¹
  | Integer.pos n => natPow x n
  | Integer.neg n => natPow (inv x) (n + 1)

  def uarec {A : Type u} (Ï† : A â‰ƒ A) : SÂ¹ â†’ Type u := rec A (ua Ï†)

  hott def Î©rec {A : Type u} (zero : A) (succ pred : A â†’ A)
    (p : succ âˆ˜ pred ~ id) (q : pred âˆ˜ succ ~ id) : (Î©Â¹(SÂ¹)) â†’ A :=
  Î» r, @transport SÂ¹ (uarec âŸ¨succ, (âŸ¨pred, qâŸ©, âŸ¨pred, pâŸ©)âŸ©) base base r zero

  section
    variable {A : Type u} (zero : A) (succ pred : A â†’ A)
             (p : succ âˆ˜ pred ~ id) (q : pred âˆ˜ succ ~ id)

    hott def Î©recÎ²â‚ : Î©rec zero succ pred p q (idp base) = zero :=
    by reflexivity

    noncomputable hott def Î©recÎ²â‚‚ (r : Î©Â¹(SÂ¹)) :
        Î©rec zero succ pred p q (r â¬ loop)
      = succ (Î©rec zero succ pred p q r) :=
    begin
      transitivity; apply transportToTransportconst;
      transitivity; apply Id.map (transportconst Â· zero);
      transitivity; apply mapFunctoriality; apply Id.map; apply recÎ²ruleâ‚‚;
      transitivity; apply transportconstOverComposition;
      transitivity; apply ua.transportRule; apply Id.map succ;
      symmetry; apply transportToTransportconst
    end

    noncomputable hott def Î©recÎ²â‚ƒ (r : Î©Â¹(SÂ¹)) :
        Î©rec zero succ pred p q (r â¬ loopâ»Â¹)
      = pred (Î©rec zero succ pred p q r) :=
    begin
      transitivity; apply transportToTransportconst;
      transitivity; apply Id.map (transportconst Â· zero);
      transitivity; apply mapFunctoriality; apply Id.map;
      transitivity; apply Id.mapInv; apply Id.map Id.symm; apply recÎ²ruleâ‚‚;
      transitivity; apply transportconstOverComposition;
      transitivity; apply transportconstOverInv;
      transitivity; apply ua.transportInvRule; apply Id.map pred;
      symmetry; apply transportToTransportconst
    end
  end

  hott def mult (p q : Î©Â¹(SÂ¹)) : Î©Â¹(SÂ¹) :=
  Id.map (rec base p) q

  hott def multZero (p : Î©Â¹(SÂ¹)) : mult p (idp base) = idp base :=
  idp (idp base)

  hott def multSucc (p q : Î©Â¹(SÂ¹)) : mult p (succ q) = mult p q â¬ p :=
  begin transitivity; apply mapFunctoriality; apply Id.map; apply recÎ²ruleâ‚‚ end

  hott def multDistrRight (p q r : Î©Â¹(SÂ¹)) : mult p (q â¬ r) = mult p q â¬ mult p r :=
  by apply mapFunctoriality

  hott def recComp (p q : Î©Â¹(SÂ¹)) : rec base p âˆ˜ rec base q ~ rec base (mult p q) :=
  begin
    fapply ind; reflexivity; change _ = _; transitivity;
    apply Equiv.transportOverHmtpy; transitivity;
    apply Id.map (Â· â¬ _ â¬ _); transitivity; apply mapInv;
    apply Id.map; transitivity; apply mapOverComp; apply Id.map (mult p); apply recÎ²ruleâ‚‚;
    transitivity; apply bimap; apply Id.reflRight; apply recÎ²ruleâ‚‚; apply Id.invComp
  end

  hott def multAssoc (p q r : Î©Â¹(SÂ¹)) : mult (mult p q) r = mult p (mult q r) :=
  begin
    symmetry; transitivity; symmetry; apply mapOverComp (rec base q) (rec base p) r;
    transitivity; apply Equiv.mapWithHomotopy; apply recComp; apply Id.reflRight
  end

  hott def mulNegRight (p q : Î©Â¹(SÂ¹)) : mult p qâ»Â¹ = (mult p q)â»Â¹ :=
  by apply Id.mapInv

  hott def mapExt {B : Type u} (Ï† : SÂ¹ â†’ B) : Ï† ~ rec (Ï† base) (Id.map Ï† loop) :=
  begin
    fapply ind; reflexivity; change _ = _; transitivity; apply Equiv.transportOverHmtpy;
    transitivity; apply bimap; transitivity; apply Id.reflRight; apply Id.mapInv;
    apply recÎ²ruleâ‚‚; apply Id.invComp
  end

  section
    variable {B : Type u} (b : B) (p q : b = b) (H : p â¬ q = q â¬ p)

    hott def birec : SÂ¹ â†’ SÂ¹ â†’ B :=
    @rec (SÂ¹ â†’ B) (rec b p) (Theorems.funext (Circle.ind q (begin
      change _ = _; transitivity; apply Equiv.transportOverHmtpy;
      transitivity; apply bimap (Â· â¬ q â¬ Â·);
      apply recÎ²ruleâ‚ƒ; apply recÎ²ruleâ‚‚;
      apply Id.idConjIfComm; exact Hâ»Â¹
    end)))

    hott def birecÎ²ruleâ‚ : Id.map (birec b p q H base) loop = p :=
    by apply recÎ²ruleâ‚‚

    hott def birecÎ²ruleâ‚‚ : Id.map (birec b p q H Â· base) loop = q :=
    begin
      transitivity; apply Interval.mapHapply;
      transitivity; apply Id.map (happly Â· base); apply recÎ²ruleâ‚‚;
      apply happly (Theorems.happlyFunext _ _ _) base
    end

    hott def birecBimap : bimap (birec b p q H) loop loop = p â¬ q :=
    begin
      transitivity; apply Equiv.bimapCharacterization';
      apply bimap; apply birecÎ²ruleâ‚; apply birecÎ²ruleâ‚‚
    end
  end

  open GroundZero.Types.Coproduct

  noncomputable hott def succNeqIdp : ua (Integer.succEquiv) â‰  idp â„¤ :=
  begin
    intro H; apply @ua.succNeqZero 0; apply @inl.encode â„• â„• _ (inl 0);
    transitivity; symmetry; apply ua.transportRule Integer.succEquiv 0;
    apply ap (transportconst Â· 0) H
  end

  noncomputable hott def helixNontriv : helix â‰  (_ â†¦ â„¤) :=
  begin
    intro H; apply succNeqIdp; transitivity; symmetry; apply recÎ²ruleâ‚‚;
    apply transport (Î» Ï†, ap Ï† loop = idp (Ï† base)) Hâ»Â¹; apply constmap
  end

  noncomputable hott def loopSpaceNontriv : Â¬(Î  (x y : SÂ¹), (x = y) â‰ƒ â„¤) :=
  begin
    intro H; apply helixNontriv; apply Theorems.funext; intro y;
    apply ua; transitivity; symmetry; apply family; apply H
  end
end Circle

def Torus := SÂ¹ Ã— SÂ¹
notation "TÂ²" => Torus

namespace Torus
  open Types.Product
  def b : TÂ² := âŸ¨Circle.base, Circle.baseâŸ©

  def injâ‚ : SÂ¹ â†’ TÂ² := Prod.mk Circle.base
  def injâ‚‚ : SÂ¹ â†’ TÂ² := (Prod.mk Â· Circle.base)

  -- poloidal and toroidal directions
  def p : b = b := prod (idp Circle.base) Circle.loop
  def q : b = b := prod Circle.loop (idp Circle.base)

  hott def Î¦ {Ï€ : Type u} {x x' y y' : Ï€}
    (Î± : x = x') (Î² : y = y') :
    prod (idp x) Î² â¬ prod Î± (idp y') =
    prod Î± (idp y) â¬ prod (idp x') Î² :=
  begin induction Î±; induction Î²; reflexivity end

  hott def t : p â¬ q = q â¬ p :=
  Î¦ Circle.loop Circle.loop
end Torus

namespace Sphere
end Sphere

namespace Glome
end Glome

end HITs

namespace Types.Integer
  noncomputable def succPath := GroundZero.ua Integer.succEquiv

  noncomputable def shift : â„¤ â†’ â„¤ = â„¤ :=
  HITs.Loop.power succPath

  noncomputable hott def shiftComp (z : â„¤) :
    shift z â¬ succPath = shift (Integer.succ z) :=
  HITs.Loop.powerComp succPath z
end Types.Integer

end GroundZero