import GroundZero.Types.Equiv

namespace GroundZero.Types
universe u v w w'

def Coproduct (A : Type u) (B : Type v) := Sum A B
infixl:65 " + " => Coproduct

attribute [eliminator] Sum.casesOn

namespace Coproduct
  variable {A : Type u} {B : Type v}

  @[matchPattern] def inl : A ‚Üí A + B := Sum.inl
  @[matchPattern] def inr : B ‚Üí A + B := Sum.inr

  hott def elim {C : Type w} (g‚ÇÄ : A ‚Üí C) (g‚ÇÅ : B ‚Üí C) : A + B ‚Üí C
  | inl a => g‚ÇÄ a
  | inr b => g‚ÇÅ b

  hott def bimap {C : Type w} {C' : Type w'} (f : A ‚Üí C) (g : B ‚Üí C') : A + B ‚Üí C + C' :=
  elim (Sum.inl ‚àò f) (Sum.inr ‚àò g)

  hott def inv : A + B ‚Üí B + A
  | inl x => inr x
  | inr x => inl x

  hott def symm : A + B ‚âÉ B + A :=
  begin
    existsi inv; apply Qinv.toBiinv; existsi inv;
    apply Prod.mk <;> { intro x; induction x using Sum.casesOn <;> reflexivity }
  end

  namespace inl
    hott def code (a‚ÇÄ : A) : A + B ‚Üí Type u
    | inl a => a‚ÇÄ = a
    | inr b => ùüé

    hott def encode {a‚ÇÄ : A} {x : A + B} (p : inl a‚ÇÄ = x) : code a‚ÇÄ x :=
    Equiv.transport (code a‚ÇÄ) p (idp a‚ÇÄ)

    hott def decode {a‚ÇÄ : A} : Œ† {x : A + B} (c : code a‚ÇÄ x), inl a‚ÇÄ = x
    | inl a, c => Id.map inl c
    | inr b, c => Proto.Empty.elim c

    hott def decodeEncode {a‚ÇÄ : A} {x : A + B}
      (p : inl a‚ÇÄ = x) : decode (encode p) = p :=
    begin induction p; reflexivity end

    hott def encodeDecode {a‚ÇÄ : A} {x : A + B} : Œ† (c : code a‚ÇÄ x), encode (decode c) = c :=
    begin
      induction x using Sum.casesOn; intro (p : a‚ÇÄ = _);
      induction p; apply idp; apply Proto.Empty.casesOn
    end

    hott def recognize (a‚ÇÄ : A) (x : A + B) : (inl a‚ÇÄ = x) ‚âÉ code a‚ÇÄ x :=
    begin
      existsi encode; apply Qinv.toBiinv; existsi decode;
      apply Prod.mk; apply encodeDecode; apply decodeEncode
    end

    hott def inj' (x y : A) : @Id (A + B) (inl x) (inl y) ‚âÉ (x = y) :=
    recognize x (inl y)

    hott def inlInr (x : A) (y : B) : @Id (A + B) (inl x) (inr y) ‚âÉ ùüé :=
    recognize x (inr y)
  end inl

  namespace inr
    hott def code (b‚ÇÄ : B) : A + B ‚Üí Type v
    | inl a => ùüé
    | inr b => b‚ÇÄ = b

    hott def encode {b‚ÇÄ : B} {x : A + B} (p : inr b‚ÇÄ = x) : code b‚ÇÄ x :=
    Equiv.transport (code b‚ÇÄ) p (idp b‚ÇÄ)

    hott def decode {b‚ÇÄ : B} : Œ† {x : A + B} (c : code b‚ÇÄ x), inr b‚ÇÄ = x
    | inl a, c => Proto.Empty.elim c
    | inr b, c => Id.map inr c

    hott def decodeEncode {b‚ÇÄ : B} {x : A + B}
      (p : inr b‚ÇÄ = x) : decode (encode p) = p :=
    begin induction p; reflexivity end

    hott def encodeDecode {b‚ÇÄ : B} {x : A + B} : Œ† (c : code b‚ÇÄ x), encode (decode c) = c :=
    begin
      induction x using Sum.casesOn; apply Proto.Empty.casesOn;
      intro (p : b‚ÇÄ = _); induction p; apply idp;
    end

    hott def recognize (b‚ÇÄ : B) (x : A + B) : (inr b‚ÇÄ = x) ‚âÉ code b‚ÇÄ x :=
    begin
      existsi encode; apply Qinv.toBiinv; existsi decode;
      apply Prod.mk; apply encodeDecode; apply decodeEncode
    end

    hott def inj' (x y : B) : @Id (A + B) (inr x) (inr y) ‚âÉ (x = y) :=
    recognize x (inr y)

    hott def inrInl (x : B) (y : A) : @Id (A + B) (inr x) (inl y) ‚âÉ ùüé :=
    recognize x (inl y)
  end inr

  hott def code {A B : Type u} : A + B ‚Üí A + B ‚Üí Type u
  | inl a => inl.code a
  | inr b => inr.code b

  hott def pathSum {A B : Type u} (z‚ÇÅ z‚ÇÇ : A + B) (p : code z‚ÇÅ z‚ÇÇ) : z‚ÇÅ = z‚ÇÇ :=
  begin
    induction z‚ÇÅ using Sum.casesOn <;> induction z‚ÇÇ using Sum.casesOn;
    apply Id.map; assumption; apply Proto.Empty.elim p;
    apply Proto.Empty.elim p; apply Id.map; assumption
  end
end Coproduct

end GroundZero.Types